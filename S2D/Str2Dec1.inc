; Процедура, осуществляющую преобразование содержимого строки, гарантированно содержащей число в десятичное знаковое число.
; Вход:
	; DS:ESI –адрес строки, содержащей число
; Выход:
	; EAX –код ошибки
	; 0 –нет ошибки
		; EBX –число
		; DS:ESI –указывает на первый символ после числа
	; 1 –неправильныйсимвол
		; DS:ESI –указывает на него
	; 2 –слишком большое число(не из диапазона -2^31..2^31-1)
		; DS:ESI –неопределено

; Примечание: 
	; Число в буфере записано корректно, в следующих случаях:
		; · Перед числом допускается только любое количество пробелов
		; · Отрицательные числа начинаются с одного символа ‘-‘. Между ним и числом пробелыне допускаются.
		; · Число состоит из цифр ‘0’- ‘9’, при этом цифра ‘0’ может стоять на первом месте только учисла 0. Число ‘-0’считается недопустимым.
		; · Число должно заканчиваться разделителем. Разделителями считаются следующие символы: пробел, запятая и символы с кодами: 0, 10, 13.
		; · Числосодержится в диапазоне -2^31..2^31-1
StrToDec			proc
					
					push edx
					
					cmp byte ptr ds:[esi], '-'
					jne @@PositiveNum
					mov edx, 1 						; Запоминаем минус
					inc esi
					
@@PositiveNum:
					call StrToUnsignedDec
					cmp eax, 1						; Некорректный символ
					je @@Error1
					cmp eax, 2						; Некорректное число
					je @@Error2
					
					cmp edx, 1						; Действуем в зависимости от знака
					jne @@ComparePositiveNimber
					cmp ebx, 2147483648 			; Модуль максимально допустимого значение для отрицательного числа (2^31)		
					ja @@Error2
					
					neg ebx							; Переводим число в отрицательное
					mov eax, 0						; Успешное завершение
					jmp @@EndOfProc
					
@@ComparePositiveNimber:
					cmp ebx, 2147483647 			; Максимально допустимое значение для положительного числа (2^31 - 1)
					ja @@Error2
					
					mov eax, 0						; Успешное завершение
					jmp @@EndOfProc
@@Error1:
					mov eax, 1
					jmp @@EndOfProc
@@Error2:
					mov eax, 2

@@EndOfProc:
					pop edx
					ret

StrToDec			endp